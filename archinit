#!/usr/bin/env zsh
setopt ERR_EXIT EXTENDED_GLOB

# ----------------------------------------------------------------------
# Storage initialization commmands
# Surface Pro 3 Dual Boot
# ----------------------------------------------------------------------
# curl -LO http://links.ethanschoonover.com/archinit


typeset -A CONFIG INSTALL INIT MKINITCPIO
typeset -a INSTALLERS

CONFIG=(
username    es
shell       zsh
hostname    palimpsest
timezone    America/Los_Angeles
locale      "en_US.UTF-8 UTF-8"
keymap      us
font        Lat2-Terminus16
drive       /dev/sda
swap_part   6
crypt_part  7
boot_label  SYSTEM
dmz_label   DMZ
swap_label  swap
crypt_part_label    cryptpart
crypt_mapped_label  cryptdev
crypt_fs_label      archlinux
modules     "i915"
hooks       "base systemd sd-encrypt sd-lvm2 sd-vconsole modconf block filesystems keyboard fsck shutdown"
)
#hooks       "base systemd sd-encrypt sd-vconsole modconf block filesystem btrfs fsck shutdown"
#boot_part   2
#dmz_part    5
# (above) base hook still here for emergency shell... need to check on when that gets changed

INSTALL=(

1,utilities     "
                 btrfs-progs
                 gnupg
                 htop
                 openssh
                 powertop
                 python
                 vim
                "

2,networking    "
                 dialog
                 iw
                 wpa_supplicant
                 rfkill
                 #
                 # no more network manager... too unstable
                 #networkmanager
                 #networkmanager-openvpn
                 #networkmanager-dmenu-git
                 #
                 # trying netctl... networkmanager seems to drop connection on the surface pro 3 a lot
                 netctl
                 ifplugd
                 wpa_supplicant
                "

4,x             "
                 xorg
                 xf86-video-intel
                 xf86-input-wacom
                 libva-intel-driver
                 libvdpau-va-gl
                 xorg-xinit
                "

5,dispmgr       "
                 lightdm
                 accountsservice
                 light-locker
                 lightdm-gtk3-greeter
                 xorg-server-xephyr
                "

3,boot          "
                 refind-efi
                 plymouth
                 ttf-dejavu
                "


6,freedesktop   "
                 d-feet
                 colord colord-gtk
                 desktop-file-utils
                "

7,wm            "
                 compton
                 dmenu
                 dunst
                 gnome-settings-daemon # may be more trouble than it's worth... right now ONLY installed to quick test if
                 # it's enough to get wacom working properly.
                 gnome-keyring
                 gnome-keyring-query
                 i3
                 ttf-font-icons
                 network-manager-applet
                 polkit-gnome
                 rxvt-unicode
                 terminus-font
                 udiskie
                 unclutter
                 volnoti
                 what other notification daemon?
                 dex-git
                 # dex is a good replacement for gtk-launch
                 desktop-file-utils #may not be necessary
                 xdg-utils #includes xdg-open... see xdg-open on arch wiki for replacement options
                "
7,fonts         "
                 # using Infinality-bundle+fonts - cf arch wiki & bbs:
                 # https://wiki.archlinux.org/index.php/Infinality-bundle%2Bfonts#Installation
                 # https://bbs.archlinux.org/viewtopic.php?id=162098
                 add the infinality-bundle repo to pacman.conf
                 add key to keyring (key ID 962DDE58)
                 install infinality-bundle meta package
                 from aur: pacman -Ss infinality-bundle-fonts-extra
                "

8,xutils        "
                 arandr
                 argyllcms
                 dispcalgui
                 feh
                 file-roller
                 p7zip
                 parcellite
                 unrar
                 unzip
                 xkeycaps
                 xorg-xfontsel
                 zip
                "
9,themes        "
                 gnome-themes-standard # (inc gtk3 themes)
                "

9,xapps         "
                 ttf-bitstream-vera
                 google-chrome-dev
                 ttf-google-fonts-git
                 
                 # firefox for hidpi support
                 firefox
                 # gstreamer stuff for firefox
                 gst-plugins-good
                 gst-libav
                 # https://addons.mozilla.org/en-US/firefox/addon/autohidpi/ for hidpi support
                "

10,sound        "
                 alsa-utils
                 pulseaudio
                 pulseaudio-alsa
                 paprefs
                 pavucontrol
                 pa-applet-git
                 OR
                 pasystray
                "
)

INIT=(
sound           'amixer sset Master unmute || :'
video           'print "export LIBVA_DRIVER_NAME=vdpau\nexport VDPAU_DRIVER=va_gl" > /etc/video_accel.sh'        
network         'systemctl enable NetworkManager; systemctl enable NetworkManager-dispatcher'
                'copy /usr/share/doc/networkmanager-dmenu-git/config.ini.example into ~/.config/networkmanager-dmenu/config.ini'
dispmgr         'lightdm.service'
wm              'ln -s .xinitrc .xsession'
boot            'refind setup...
                 systemctl disable lightdm; systemctl enable lightdm-plymouth'
modules         'print hid-multitouch > /etc/modules-load.d/hid-multitouch.conf'
)

# purely cosmetic
setfont $CONFIG[font]

TRAPZERR () { print "\nUNHANDLED ERROR"; exit }

:<< \
------------------------------------------------------------------------
Storage configuration
------------------------------------------------------------------------

# partitioning (drive contents preserved)
# ----------------------------------------------------------------------
ram="$(( $(print ${$(cat /proc/meminfo | grep -i memtotal)[2]}) / 1000000 ))G"
while ! ${${${pass::=$(read -Ers "?Passphrase: ")}:#$(\
    read -Ers "?$(print "\nConfirmation: ")")}:+false};
do print "\nDidn't match--try again:\n"; done; print
sgdisk --new=0:0:0 --change-name=0:cryptlvm /dev/sda
print -r $pass | cryptsetup luksFormat /dev/disk/by-partlabel/cryptlvm
print -r $pass | cryptsetup open /dev/disk/by-partlabel/cryptlvm lvm
pvcreate /dev/mapper/lvm # physical volume
vgcreate storage /dev/mapper/lvm # volume group
lvcreate -L 8G storage  -n swap # logical volume
lvcreate -l 100%FREE storage -n system # logical volume
mkfs.btrfs --label system /dev/mapper/storage-system # make filesystem
mkswap -L swap /dev/mapper/storage-swap # make swap device
swapon -d -L swap # activate swap device
# OR swapon -d /dev/storage/swapvol # activate swap device
mount -t btrfs /dev/storage/system /mnt # temp mount for subvol creation
btrfs subvolume create /mnt/root
btrfs subvolume set-default ${$(btrfs subvolume list /mnt)[(w)2]} /mnt
btrfs subvolume create /mnt/home
umount -R /mnt
o=defaults,x-mount.mkdir
o=defaults,x-mount.mkdir
o_btrfs=$o,compress=lzo,space_cache,autodefrag,inode_cache,ssd,discard
mount -t btrfs -o subvol=root,$o_btrfs /dev/storage/system /mnt
mount -t btrfs -o subvol=home,$o_btrfs /dev/storage/system /mnt/home
mount -o $o -L SYSTEM /mnt/boot
mount -o $o -L DMZ /mnt/home/dmz

# Mount filesystems and subvolumes
# ----------------------------------------------------------------------

:<< \
------------------------------------------------------------------------
Clean up old initramfs files
------------------------------------------------------------------------
#rm -f /mnt/boot/*(#q.)
rm -f /mnt/boot/{initramfs-linux-fallback.img,initramfs-linux.img,vmlinuz-linux}

:<< \
------------------------------------------------------------------------
Install base system
------------------------------------------------------------------------
pacman -Sy
pacstrap /mnt base
pacstrap /mnt base-devel
pacstrap /mnt zsh
genfstab -L -p /mnt > /mnt/etc/fstab
genfstab -U -p /mnt > /mnt/etc/fstab.uuid

:<< \
------------------------------------------------------------------------
Dump remainder into post chroot script
------------------------------------------------------------------------
# extract everything after this section with necessary variables prefixed
head -n3 $0 > /mnt/installer
typeset -p CONFIG INIT INSTALL MKINITCPIO >> /mnt/installer
sed -n "/>\{10\}/,$ p" "$0" >> /mnt/installer
chmod +x /mnt/installer
arch-chroot /mnt /installer; rm /mnt/installer;
#umount -R /mnt/boot; umount -R /mnt; cryptsetup close $CONFIG[crypt_mapped_label]
print "Installation complete. Check boot manager and reboot."
exit

# >>>>>>>>>>>>>>>>>>>>>>>> CHROOT TRIM LINE <<<<<<<<<<<<<<<<<<<<<<<<<<<<

:<< \
------------------------------------------------------------------------
Install Commands
------------------------------------------------------------------------
tmpaur=/tmp/aur
[[ -d $tmpaur ]] && rm -rf $tmpaur; mkdir $tmpaur
() {
    for package
        do
        cd $tmpaur
        curl -O https://aur.archlinux.org/packages/${package:0:2}/${package}/${package}.tar.gz
        tar -xzvf ${package}.tar.gz
        cd $tmpaur/${package}
        makepkg --asroot --syncdeps --install --noconfirm
        cd
        rm -rf $tmpaur/${package}
    done
} aura-bin #cower pacaur
cd
installpkg () {
    for package in ${=*}
    do
        pacman --noconfirm -S $package || su $CONFIG[username] -c "sudo aura --noconfirm -A $package"
    done
}

:<< \
------------------------------------------------------------------------
Locale, Console Font, Timezone, Hostname
------------------------------------------------------------------------
print "Setting locale-gen: ${CONFIG[locale]}"
print ${CONFIG[locale]} > /etc/locale.gen && locale-gen
print "Setting locale.conf: ${CONFIG[locale][(w)1]}"
print ${CONFIG[locale][(w)1]} >> /etc/locale.conf
export LANG=${CONFIG[locale][(w)1]}
print "KEYMAP=${CONFIG[keymap]}\nFONT=${CONFIG[font]}"
print "KEYMAP=${CONFIG[keymap]}\nFONT=${CONFIG[font]}" > /etc/vconsole.conf
ln -sf "/usr/share/zoneinfo/${CONFIG[timezone]}" /etc/localtime && hwclock --systohc --utc
print "Hostname: ${CONFIG[hostname]}"
print ${CONFIG[hostname]} > /etc/hostname
insert_at=${"${(@A)hosts::="${(@f):-$(</etc/hosts)}"}"[(I)127*]}
hosts[$insert_at]="${hosts[insert_at]} ${CONFIG[hostname]}"
print -l -- "${(@)hosts}"
print -l -- "${(@)hosts}" > /etc/hosts

:<< \
------------------------------------------------------------------------
Initial User & sudo
------------------------------------------------------------------------
useradd -m -G wheel -s /usr/bin/zsh $CONFIG[username]
echo "%wheel ALL=(ALL) NOPASSWD: ALL" > /var/tmp/sudoers.wheel
visudo -cf /var/tmp/sudoers.wheel \
    && { mv /var/tmp/sudoers.wheel /etc/sudoers.d/wheel } \
    || { print "ERROR updating sudoers; no change made" }

:<< \
------------------------------------------------------------------------
Initial Ramdisk
------------------------------------------------------------------------
installpkg btrfs-progs
# TODO: rm old initrd files
mv /etc/mkinitcpio.conf /etc/mkinitcpio.conf.orig
print "MODULES=\"${=CONFIG[modules]}\"" > /etc/mkinitcpio.conf
print "HOOKS=\"${=CONFIG[hooks]}\"" >> /etc/mkinitcpio.conf
mkinitcpio -p linux || :

:<< \
------------------------------------------------------------------------
Bootloader & Kernel Command Line
------------------------------------------------------------------------
# TODO:
# if there is no existing /boot/EFI/refind directory, copy the refind
# default directory to /boot/EFI/refind
# if desired theme is missing, install it as well
#
# append a new Archlinu menuentry 
newentry="\n\nmenuentry 'New Arch Linux' {
    icon EFI/refind/next-theme/icons/os_arch.png
    loader vmlinuz-linux
    initrd initramfs-linux.img
    options 'ro rd.luks.crypttab=0 rd.luks.allow-discards rd.luks.uuid=${CONFIG[crypt_uuid]} root=UUID=${CONFIG[root_uuid]} rootflags=subvol=root,rw,x-systemd.device-timeout=0,relatime,compress=lzo,ssd,space_cache,autodefrag,inode_cache i915.modeset=1 quiet vga=current'
    enabled
}"
print "$newentry" >> /boot/EFI/refind/refind.conf

:<< \
------------------------------------------------------------------------
Installers
------------------------------------------------------------------------
for installer in ${(kn)INSTALL}
do
    [[ -n ${install::=${INSTALL[$installer]:-}} ]] && installpkg $install
    [[ -n ${init::=${INIT[${installer##[0-9]##,}]:-}} ]] && eval $init
done

:<< \
------------------------------------------------------------------------
Passwords
------------------------------------------------------------------------
# Query and confirm password changes for root and user account
() {
    for user
    do
        print "Password for account '$user'"
        while ! ${${${pass::=$(read -Ers "?Password: ")}:#$(\
            read -Ers "?$(print "\nConfirmation: ")")}:+false};
        do print "\nDidn't match--try again:\n"; done; print
        print -r $user:$pass | chpasswd
    done
} root $CONFIG[username]


:<< \
------------------------------------------------------------------------
End of chroot section
------------------------------------------------------------------------
# will exit from chroot and perform cleanup


# vim: set filetype=sh :
